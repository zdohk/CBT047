         TITLE 'JES2 EXIT 2 ENFORCES JOBNAME AND CLASS STANDARD'
*        PROD JOBS XXXANNNO
*                  XXX      - SYSTEM ID
*                     A     - T FOR TEST, P FOR PRODUCTION
*                      NNN  - JOB NUMBER
*                         O - OPTIONAL CHARACTER
*        LP JOBS   XXNNNNAO
*                  XX       - SYSTEM ID- LP
*                    N      - MODEL NUMBER 1,2,3,OR 4
*                     NNN   - JOB NUMBER
*                        A  -  P FOR PRODUCTION
*                         O - OPTIONAL CHARACTER
*        TEST JOBS XXXXXXAA
*                  XXXXXX   - TSO USER ID
*                        AA - UNIQUE CHARACTERS
*
*        FALPNNN  - CLASS Q
*        ADAP001  - CLASS Q
*        ADAI001  - CLASS U
*        ADAT001  - CLASS R
*        ADAPNNN  - CLASS S
*        ADAINNN  - CLASS S
*        ADATNNN  - CLASS B
*        FALTNNN  - CLASS R
*        XXXPNNN  - CLASS A
*        XXXTNNN  - CLASS B
*        LP1NNNP  - CLASS K OR G
*        LP2NNNP  - CLASS L OR H
*        LP3NNNP  - CLASS M OR I
*        LP4NNNP  - CLASS N OR J
*        TSGXXAA  - CLASS Z
*        DEVXXAA  - CLASS B
*        OPSXXAA  - CLASS B
*        SECXXAA  - CLASS B
*        IFOXXAA  - CLASS B
*        DRMXXAA  - CLASS B
*        CDSXXAA  - CLASS B
*        PASXXAA  - CLASS B
*        XXXXXAA  - CLASS A
*        GLXPNNN  - CLASS 4
*        GLMPNNN  - CLASS 4
*        EXPPNNN  - CLASS A OR 4
*        GASPNNN  - CLASS A OR 4
*
         COPY  $HASPGBL
EXIT02   $MODULE RPL,                                                  X
               $JQE,                                                   X
               $PCE,                                                   X
               $HASPEQU,                                               X
               $LRC,                                                   X
               $RDRWORK,                                               X
               $BUFFER,                                                X
               $JCT,                                                   X
               $PADDR,                                                 X
               $CAT,                                                   X
               $HCT,                                                   X
               $MIT
****************    END CONTROL BLOCKS      **************************
         EJECT
EXIT002  $ENTRY CSECT=YES
         $SAVE
         LR     R8,R15      LOAD BASE REGISTER
         L      R4,0(R1)    POINT R4 TO CARD IMAGE
         LTR    R0,R0       TEST FOR CONTINUATION CARD
         BNZ    CONTCARD
NEWJOB   MVC    PCEUSER0,ZERO
         CLC    TESTINIT,2(R4)   TEST FOR INIT
         BE     FINISH
         CLC    TESTLOG,2(R4)   TEST FOR SYSLOG
         BE     FINISH
         CLI    3(R4),C'Z'      CHAR COL4  JES CHECKS FOR CHAR
         BH     BADJOB                     COL 3 SO NO NEED
         CLI    4(R4),C'Z'      CHAR COL5 IF NOT THEN LP
         BH     LPJOB
TESTJOB  CLI    6(R4),C'Z'      CHAR COL7
         BH     PRODJOB
         CLC    JOBTSG,2(R4)    TEST FOR TSGXXNN
         BNE    DEV
         MVI    PCEUSER0,C'Z'      CLASS Z FOR TSG
         B      SETCLASS        GO SET CLASS
DEV      CLC    JOBDEV,2(R4)    TEST FOR DEVXXNN
         BNE    SPA
         MVI    PCEUSER0,C'B'   CLASS B FOR DEV
         B      SETCLASS
SPA      CLC    JOBSPA,2(R4)    TEST FOR SPACIDN
         BNE    IFO
         MVI    PCEUSER0,C'B'   CLASS B FOR DEV
         B      SETCLASS
IFO      CLC    JOBIFO,2(R4)    TEST FOR IFOXXNN
         BNE    DRM
         MVI    PCEUSER0,C'B'   CLASS B FOR DEV
         B      SETCLASS
DRM      CLC    JOBDRM,2(R4)    TEST FOR DRMXXNN
         BNE    CDS
         MVI    PCEUSER0,C'B'   CLASS B FOR DRM
         B      SETCLASS
CDS      CLC    JOBCDS,2(R4)    TEST FOR CDSXXNN
         BNE    PAS
         MVI    PCEUSER0,C'B'   CLASS B FOR PAS
         B      SETCLASS
PAS      CLC    JOBPAS,2(R4)    TEST FOR PASXXNN
         BNE    OPS
         MVI    PCEUSER0,C'B'   CLASS B FOR PAS
         B      SETCLASS
OPS      CLC    JOBOPS,2(R4)    TEST FOR OPSXXNN
         BNE    SEC
         MVI    PCEUSER0,C'B'   CLASS B FOR OPS
         B      SETCLASS
SEC      CLC    JOBSEC,2(R4)    TEST FOR SECXXNN
         BNE    PPA
         MVI    PCEUSER0,C'B'   CLASS B FOR SEC
         B      SETCLASS
PPA      CLC    JOBPPA,2(R4)    TEST FOR PPAXXXLN
         BNE    REF
         CLI    8(R4),C'L'      CHECK FOR L IN COL 9
         BE     LPJOBTSO
         B      USR             ELSE STRAIGHT USER JOB
REF      CLC    JOBREF,2(R4)    TEST FOR REFXXXLN
         BNE    USR
         CLI    8(R4),C'L'      CHECK FOR L IN COL 9
         BE     LPJOBTSO        ELSE STRAIGHT USER JOB
USR      CLI    5(R4),C'Z'      CHAR COL6
         BH     BADJOB
         CLI    7(R4),C' '      Â¬= BLANK COL 9 MEANS XXXXXXAA USER JOB
         BE     BADJOB
         MVI    PCEUSER0,C'A'   CLASS A FOR USER JOB
         B      SETCLASS
LPJOB    CLC    JOBLP,2(R4)     TEST FOR LPXNNNP
         BNE    BADJOB
         CLI    5(R4),C'Z'      NUMERIC COL6
         BNH    BADJOB
         CLI    6(R4),C'Z'      NUMERIC COL7
         BNH    BADJOB
         CLI    7(R4),C'Z'      NUMERIC COL8
         BNH    BADJOB
         CLI    8(R4),C'P'      PROD COL9
         BNE    BADJOB
LPCLASS1 CLI    4(R4),C'1'
         BNE    LPCLASS2
         MVI    PCEUSER0,C'1'
         B      SETCLASS
LPCLASS2 CLI    4(R4),C'2'
         BNE    LPCLASS3
         MVI    PCEUSER0,C'2'
         B      SETCLASS
LPCLASS3 CLI    4(R4),C'3'
         BNE    LPCLASS4
         MVI    PCEUSER0,C'3'
         B      SETCLASS
LPCLASS4 CLI    4(R4),C'4'
         BNE    BADJOB
         MVI    PCEUSER0,C'4'
         B      SETCLASS
LPJOBTSO CLI    9(R4),C'1'      PPAXXXL1 REFXXXL1
         BNE    LPTSO2
         MVI    PCEUSER0,C'1'
         B      SETCLASS
LPTSO2   CLI    9(R4),C'2'      PPAXXXL2 REFXXXL2
         BNE    LPTSO3
         MVI    PCEUSER0,C'2'
         B      SETCLASS
LPTSO3   CLI    9(R4),C'3'      PPAXXXL3 REFXXXL3
         BNE    LPTSO4
         MVI    PCEUSER0,C'3'
         B      SETCLASS
LPTSO4   CLI    9(R4),C'4'      PPAXXXL4 REFXXXL4
         BNE    BADJOB
         MVI    PCEUSER0,C'4'
         B      SETCLASS
PRODJOB  CLI    7(R4),C'Z'      NUMERIC COL8
         BNH    BADJOB
         CLI    8(R4),C'Z'      NUMERIC COL9
         BNH    BADJOB
         CLI    5(R4),C'R'      RERUN COL 6
         BE     JOBPROD
         CLI    5(R4),C'P'      PROD COL6
         BNE    PRODTEST
FALPROD  CLC    JOBFAL,2(R4)    TEST FOR FALNNNP
         BNE    EXPPROD
         MVI    PCEUSER0,C'Q'      CLASS Q FOR PROD FALCON
         B      SETCLASS        GO SET CLASS
EXPPROD  CLC    JOBEXP,2(R4)    *** TEST FOR EXPP
         BNE    GASPROD
         MVI    PCEUSER0,C'*'      SET CLASS TO * TO INDICATE THIS
         B      SETCLASS           IS SPECIAL EXP OR GAS JOB
GASPROD  CLC    JOBGAS,2(R4)    *** TEST FOR EXPP
         BNE    GLXPROD
         MVI    PCEUSER0,C'*'      SET CLASS TO * TO INDICATE THIS
         B      SETCLASS           IS SPECIAL EXP OR GAS JOB
GLXPROD  CLC    JOBGLX,2(R4)    TEST FOR GLXPNNN
         BNE    GLMPROD
         MVI    PCEUSER0,C'+'      CLASS 4 FOR PROD GLX
         B      SETCLASS        GO SET CLASS
GLMPROD  CLC    JOBGLM,2(R4)    TEST FOR GLMPNNN
         BNE    ADAPROD
         MVI    PCEUSER0,C'+'      CLASS 4 FOR PROD GLM
         B      SETCLASS        GO SET CLASS
ADAPROD  CLC    JOBADA1,2(R4)    TEST FOR ADAP001
         BNE    ADAOTHER
         MVI    PCEUSER0,C'Q'      CLASS Q FOR PROD ADABAS
         B      SETCLASS        GO SET CLASS
ADAOTHER CLC    JOBADA,2(R4)    TEST FOR ADA
         BNE    JOBPROD
         MVI    PCEUSER0,C'S'      CLASS S FOR ADABAS
         B      SETCLASS        GO SET CLASS
JOBPROD  MVI    PCEUSER0,C'A'   CLASS A FOR PROD
         B      SETCLASS
PRODTEST CLI    5(R4),C'T'      PROD COL6
         BNE    PRODIMPL
ADATEST  CLC    JOBADA2,2(R4)    TEST FOR ADAT001
         BNE    ADATXXX
         MVI    PCEUSER0,C'R'      CLASS R FOR TEST ADABAS
         B      SETCLASS        GO SET CLASS
ADATXXX  CLC    JOBADA,2(R4)    TEST FOR ADA
         BNE    FALTEST
         B      JOBTEST
FALTEST  CLC    JOBFAL,2(R4)    TEST FOR FALTNNN
         BNE    JOBTEST
         MVI    PCEUSER0,C'R'      CLASS R FOR TEST FALCON
         B      SETCLASS        GO SET CLASS
JOBTEST  MVI    PCEUSER0,C'B'   CLASS B FOR PROD TESTING
         B      SETCLASS
PRODIMPL CLI    5(R4),C'I'      PROD COL6
         BNE    BADJOB
ADAIMPL  CLC    JOBADA3,2(R4)    TEST FOR ADAI001
         BNE    ADAIXXX
         MVI    PCEUSER0,C'U'      CLASS U FOR IMPL ADABAS
         B      SETCLASS        GO SET CLASS
ADAIXXX  CLC    JOBADA,2(R4)    TEST FOR ADA
         BNE    BADJOB
         MVI    PCEUSER0,C'S'      CLASS S FOR ADABAS
SETCLASS LA     R2,2(R4)
         SR     R3,R3
         LA     R3,3(R3)
TEST     CLC    CLASS1,0(R2)
         BE     REPLACE
         CLC    CLASS2,0(R2)
         BE     REPLACE
         LA     R3,1(R3)
         LA     R2,1(R2)
         CL     R3,ENDCARD
         BNE    TEST
         B      FINISH
REPLACE  CLI    PCEUSER0,C'+'    + IS GLX OR GLM JOB
         BE     GLXJOB
LP1      CLI    PCEUSER0,C'1'
         BNE    LP2
         CLI    7(R2),C'K'
         BE     LPFIN
         CLI    7(R2),C'G'
         BE     LPFIN
         B      BADJOB
LP2      CLI    PCEUSER0,C'2'
         BNE    LP3
         CLI    7(R2),C'L'
         BE     LPFIN
         CLI    7(R2),C'H'
         BE     LPFIN
         B      BADJOB
LP3      CLI    PCEUSER0,C'3'
         BNE    LP4
         CLI    7(R2),C'M'
         BE     LPFIN
         CLI    7(R2),C'I'
         BE     LPFIN
         B      BADJOB
LP4      CLI    PCEUSER0,C'4'
         BNE    GASOREXP
         CLI    7(R2),C'N'
         BE     LPFIN
         CLI    7(R2),C'J'
         BE     LPFIN
GASOREXP CLI    PCEUSER0,C'*'    * IS GASP OR EXPP JOB
         BNE    REGJOB
         CLI    7(R2),C'A'       ALLOWED CLASSES ARE A OR 4 FOR
         BE     LPFIN            GAS AND EXP
         CLI    7(R2),C'4'
         BE     LPFIN
         B      BADJOB
GLXJOB   MVI    PCEUSER0,C'4'      SET CLASS 4
REGJOB   MVC    7(1,R2),PCEUSER0   OTHERWISE MOVE IN CLASS
         B      LPFIN
BADJOB   MVI    PCEUSER0+1,X'FF'
         B      FINISH
CONTCARD CLI    PCEUSER0,X'00'
         BNE    SETCLASS
         B      FINISH
LPFIN    MVI    PCEUSER0,X'00'
FINISH   LA     R15,0
         $RETURN RC=(R15)
         DS     0F
ENDCARD  DC     X'0000004C'    COL 75 CLASS=Z NOT FIT ON CARD
JOBTSG   DC     CL3'TSG'
JOBSPA   DC     CL3'SPA'
JOBOPS   DC     CL3'OPS'
JOBIFO   DC     CL3'IFO'
JOBDRM   DC     CL3'DRM'
JOBCDS   DC     CL3'CDS'
JOBPAS   DC     CL3'PAS'
JOBUSR   DC     CL3'USR'
JOBDEV   DC     CL3'DEV'
JOBPPA   DC     CL3'PPA'
JOBREF   DC     CL3'REF'
JOBGLX   DC     CL3'GLX'
JOBGLM   DC     CL3'GLM'
JOBGAS   DC     CL4'GASP'
JOBEXP   DC     CL4'EXPP'
JOBSEC   DC     CL3'SEC'
JOBFAL   DC     CL3'FAL'
JOBADA   DC     CL3'ADA'
JOBADA1  DC     CL7'ADAP001'
JOBADA2  DC     CL7'ADAT001'
JOBADA3  DC     CL7'ADAI001'
JOBLP    DC     CL2'LP'
TESTINIT DC     CL4'INIT'
TESTLOG  DC     CL6'SYSLOG'
CLASS1   DC     CL7',CLASS='
CLASS2   DC     CL7' CLASS='
         DS     0F
ZERO     DC     XL4'00000000'
         $MODEND
         END
         TITLE 'JES2 EXIT 3 CANCELS JOBS FLAGGED BY EXIT02'
*        EXIT02 CANNOT DISTINGUISH BETWEEN BATCH JOBS AND STC'S
*        SO CANCEL IS DONE IS EXIT03
         COPY  $HASPGBL
* USE $MODULE WITH PARAMETERS TO INCLUDE ALL JES2 CONTROL BLOCKS
EXIT03   $MODULE RPL,                                                  X
               $PADDR,                                                 X
               $JQE,                                                   X
               $PCE,                                                   X
               $HASPEQU,                                               X
               $LRC,                                                   X
               $RDRWORK,                                               X
               $BUFFER,                                                X
               $JCT,                                                   X
               $CAT,                                                   X
               $HCT,                                                   X
               $MIT
****************    END CONTROL BLOCKS      **************************
         USING JCT,R10
         EJECT
EXIT003  $ENTRY CSECT=YES
         $SAVE
         LR     R8,R15    ESTABLISH BASE REG
         CLI    JCTJOBID,C'J'          TEST FOR JOB
         BNE    FINISH                 IF NOT END
         CLI    PCEUSER0+1,X'FF'       TEST FOR EXIT2 CANCEL FLAG
         BNE    FINISH
         MVC    JCTXWRK,MSG1
         OI     RDWFLAGX,RDWXXSEM
         LA     R15,12
         B      BADFIN
FINISH   LA     R15,0
BADFIN   $RETURN RC=(R15)
         DS     0F
MSG1     DC    CL80'JOB CANCELLED BY JES2 EXIT:JOBNAME NOT TO STANDARD'
         $MODEND
         END
         TITLE 'JES2 EXIT 4 TO DUMMY OUT JOBCAT AND STEPCAT'
*        JOBCAT AND STEPCAT DD STATEMENTS WILL BE COMMENTED
*        OUT UNLESS THE WORD TSG APPEARS IN COLUMN 65
         COPY  $HASPGBL
EXIT04   $MODULE RPL,                                                  X
               $JQE,                                                   X
               $PCE,                                                   X
               $HASPEQU,                                               X
               $LRC,                                                   X
               $RDRWORK,                                               X
               $PADDR,                                                 X
               $BUFFER,                                                X
               $JCT,                                                   X
               $CAT,                                                   X
               $HCT,                                                   X
               $MIT
****************    END CONTROL BLOCKS      **************************
         EJECT
EXIT004  $ENTRY CSECT=YES
         $SAVE
         LR     R8,R15      LOAD BASE REGISTER
         LTR    R0,R0
         BNZ    JCLTHEN
FINISH   LA     R15,0
         $STORE R15
         $RETURN
JCLTHEN  L      R12,0(R1)
         CLC    JOBCAT,0(R12)
         BE     TSGTEST
         CLC    STEPCAT,0(R12)
         BNE    TESTCONC
         CLC    TSG,64(R12)
         BNE    REPLAC2
TSGTEST  CLC    TSG,64(R12)
         BNE    REPLACE
         MVC    64(3,R12),ZAPTSG
         B      FINISH
REPLACE  MVC    0(80,R12),MSG1
         B      SETEMP
REPLAC2  MVC    0(80,R12),MSG3
SETEMP   MVI    RDWTEMP,X'FF'
         B      FINISH
TESTCONC CLI    RDWTEMP,X'FF'
         BNE    FINISH
         CLC    SLASH,0(R12)
         BE     FIRSTEST
         MVI    RDWTEMP,X'00'
         B      FINISH
FIRSTEST LA     R2,3(R12)
BLNKTEST CLC    BLANK,0(R2)
         BNE    DDTEST
         LA     R2,1(R2)
         CL     R2,EIGHTYI
         BNE    BLNKTEST
         MVI    RDWTEMP,X'00'
         B      FINISH
DDTEST   CLC    DD,0(R2)
         BE     DELCONCT
         MVI    RDWTEMP,X'00'
         B      FINISH
DELCONCT MVC    0(80,R12),MSG2
         B      FINISH
JOBCAT   DC     CL8'//JOBCAT'
STEPCAT  DC     CL9'//STEPCAT'
TSG      DC     CL3'TSG'
ZAPTSG   DC     CL3'   '
DD       DC     CL2'DD'
BLANK    DC     CL1' '
SLASH    DC     CL3'// '
         DS     0F
EIGHTYI  DC     X'00000051'
MSG1     DC     CL80'//* JOBCAT STATEMENT REMOVED BY JES2 EXIT'
MSG2     DC     CL80'//* CONCATENATED CATALOG REMOVED BY JES2 EXIT'
MSG3     DC     CL80'//* STEPCAT STATEMENT REMOVED BY JES2 EXIT'
         $MODEND
         END
         TITLE 'MPFABEND - ISSUES NONE DELETABLE MESSAGE FOR PROD JOBS'
MPFABEND CSECT
MPFABEND AMODE 31
MPFABEND RMODE ANY
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)        SAVE CALLERS REGS
         BALR  R12,R0                 ESTABLISH BASE
         USING *,R12
         L     R5,0(R1)               --> CTXT
         USING CTXT,R5
         GETMAIN RU,LV=DATAEND,LOC=BELOW,SP=230
         LR    R11,R1                 --> GETMAINED AREA
         USING DATAAREA,R11
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         LR    R13,R15
         BAL   R14,ISSUWTO            ISSUE THE MESSAGE
FINISHED EQU   *
         L     R13,4(R13)
         FREEMAIN RU,LV=DATAEND,A=(R11),SP=230 FREE THE STORAGE
         LM    R14,R12,12(R13)        RESTORE CALLERS REGS
         LA    R15,0                  SET RETURN CODE
         BR    R14                    GO BACK TO CALLER
ISSUWTO  DS    0H
         SR    R10,R10                CLEAR FLAG
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2
         LA    R4,CTXTTMSG
         USING MSGTEXT,R4
         MVC   USERAUTO,USERSTAT
*
* MOVE IN JOBNAME
*
         SLR   R6,R6                        LENGTH OF JOBNAME
         LA    R7,8                         MAXIMUM JOB NAME LENGTH
         LA    R8,USERAUTO+16               TARGET FOR JOBNAME
         LA    R9,JOBDATA                   START OF JOBNAME IN MESSAGE
JOBLOOP  CLI   0(R9),C' '                   END OF JOBNAME?
         BE    JOBTYPE
         MVC   0(1,R8),0(R9)                MOVE 1 CHARACTER AT A TIME
         LA    R6,1(,R6)                    ADD 1 TO LENGTH
         LA    R8,1(,R8)                    BUMP
         LA    R9,1(,R9)                          POINTERS
         BCT   R7,JOBLOOP                   MAX JOBNAME?
*
* DETERMINE IF IT A PRODUCTION NAME
*
JOBTYPE  C     R6,=F'7'                     MUST BE AT LEAST 7 LONG
         BL    BYPASS                       IF LESS BYPASS
         LA    R8,USERAUTO+16               JOBNAME
         LA    R7,3                         FIRST THREE ARE CHARS
CHARLOOP CLI   0(R8),C'Z'                   CHAR OR NUM?
         BH    BYPASS                       NUM BYPASS
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
         BCT   R7,CHARLOOP                  CHECK ALL THREE
         LA    R7,3                         FIRST THREE ARE CHARS
         CLI   0(R8),C'P'                   IS IT NEW PROD FORMAT?
         BNE   NUMLOOP
NUMLOOPA L     R10,FLAG                     SET FLAG FOR NEW JOB
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
NUMLOOP  CLI   0(R8),C'Z'                   CHAR OR NUM?
         BNH   BYPASS                       CHAR BYPASS
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
         BCT   R7,NUMLOOP                   CHECK ALL THREE
         CL    R10,FLAG                     IS IT NEW JOB
         BE    JOBWTO
         CLI   0(R8),C'P'                   7TH CHAR A 'P'
         BNE   BYPASS
         DROP  R2,R4
JOBWTO   WTO   MF=(E,USERAUTO)
BYPASS   BR    R14
         DS    0F
FLAG     DC    X'FFFFFFFF'
USERSTAT WTO   'SUN001I JOB          ABENDED',ROUTCDE=(15),DESC=(11),MFX
               =L
ABMSG    EQU   *-USERSTAT
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0F
USERAUTO DS    CL(ABMSG)
DATAEND  EQU   *-DATAAREA
MSGTEXT  DSECT
MSGID    DS    CL8
JOBDATA  DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         IEZVX100
         END
         TITLE 'MPFHOLD  - REPLIES NOHOLD TO IEF433D REPLY HOLD '
MPFHOLD  CSECT
MPFHOLD  AMODE 31
MPFHOLD  RMODE ANY
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)        SAVE CALLERS REGS
         BALR  R12,R0                 ESTABLISH BASE
         USING *,R12
         L     R5,0(R1)        --> CTXT
         USING CTXT,R5
         GETMAIN RU,LV=DATAEND,SP=230,LOC=BELOW
         LR    R11,R1                 --> GETMAINED AREA
         USING DATAAREA,R11
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         LR    R13,R15
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2
         LA    R4,CTXTTMSG
         USING MSGTEXT,R4
         OI    CTXTRFB2,CTXTRHCO
         XC    MGCRPL(MGCRLTH),MGCRPL
         MVC   MGCRTEXT(L'TXTINSRT),TXTINSRT
         MVC   REPLY,CTXTRPID
         LA    R1,(MGCRTEXT-MGCRPL)+L'TXTINSRT
         STC   R1,MGCRLGTH
         SR    R0,R0
         MGCR  MGCRPL
         DROP  R2,R4
FINISH   L     R13,4(R13)
         FREEMAIN RU,LV=DATAEND,SP=230,A=(R11)
         LM    R14,R12,12(R13)        RESTORE CALLERS REGS
         BR    R14                    GO BACK TO CALLER
TXTINSRT DC    CL9'XX,NOHOLD'
WAITTST  DC    CL14'WAIT REQUESTED'
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0F
MGCR     IEZMGCR  DSECT=NO
         ORG   MGCRTEXT
REPLY    DS    CL2
REPLYMSG DS    CL2
         ORG
DATAEND  EQU   *-DATAAREA
MSGTEXT  DSECT
MSGID    DS    CL8
JOBNAME  DS    CL11
WAITC    DS    CL14
         IEZVX100
         END   MPFHOLD
         TITLE 'MPFJCLER - ISSUES NONE DELETABLE MESSAGE FOR PROD JOBS'
MPFJCLER CSECT
MPFJCLER AMODE 31
MPFJCLER RMODE ANY
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)        SAVE CALLERS REGS
         BALR  R12,R0                 ESTABLISH BASE
         USING *,R12
         L     R5,0(R1)               --> CTXT
         USING CTXT,R5
         GETMAIN RU,LV=DATAEND,LOC=BELOW,SP=230
         LR    R11,R1                 --> GETMAINED AREA
         USING DATAAREA,R11
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         LR    R13,R15
         BAL   R14,ISSUWTO            ISSUE THE MESSAGE
FINISHED EQU   *
         L     R13,4(R13)
         FREEMAIN RU,LV=DATAEND,A=(R11),SP=230 FREE THE STORAGE
         LM    R14,R12,12(R13)        RESTORE CALLERS REGS
         LA    R15,0                  SET RETURN CODE
         BR    R14                    GO BACK TO CALLER
ISSUWTO  DS    0H
         SR    R10,R10                CLEAR FLAG
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2
         LA    R4,CTXTTMSG
         USING MSGTEXT,R4
         MVC   USERAUTO,USERSTAT
*
* MOVE IN JOBNAME
*
         SLR   R6,R6                        LENGTH OF JOBNAME
         LA    R7,8                         MAXIMUM JOB NAME LENGTH
         LA    R8,USERAUTO+16               TARGET FOR JOBNAME
         LA    R9,JOBDATA                   START OF JOBNAME IN MESSAGE
JOBLOOP  CLI   0(R9),C' '                   END OF JOBNAME?
         BE    JOBTYPE
         MVC   0(1,R8),0(R9)                MOVE 1 CHARACTER AT A TIME
         LA    R6,1(,R6)                    ADD 1 TO LENGTH
         LA    R8,1(,R8)                    BUMP
         LA    R9,1(,R9)                          POINTERS
         BCT   R7,JOBLOOP                   MAX JOBNAME?
*
* DETERMINE IF IT A PRODUCTION NAME
*
JOBTYPE  C     R6,=F'7'                     MUST BE AT LEAST 7 LONG
         BL    BYPASS                       IF LESS BYPASS
         LA    R8,USERAUTO+16               JOBNAME
         LA    R7,3                         FIRST THREE ARE CHARS
CHARLOOP CLI   0(R8),C'Z'                   CHAR OR NUM?
         BH    BYPASS                       NUM BYPASS
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
         BCT   R7,CHARLOOP                  CHECK ALL THREE
         LA    R7,3                         FIRST THREE ARE CHARS
         CLI   0(R8),C'P'                   IS IT NEW PROD FORMAT?
         BNE   NUMLOOP
NUMLOOPA L     R10,FLAG                     SET FLAG FOR NEW JOB
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
NUMLOOP  CLI   0(R8),C'Z'                   CHAR OR NUM?
         BNH   BYPASS                       CHAR BYPASS
         LA    R8,1(,R8)                    BUMP TO NEXT CHAR
         BCT   R7,NUMLOOP                   CHECK ALL THREE
         CL    R10,FLAG                     IS IT NEW JOB
         BE    JOBWTO
         CLI   0(R8),C'P'                   7TH CHAR A 'P'
         BNE   BYPASS
         DROP  R2,R4
JOBWTO   WTO   MF=(E,USERAUTO)
BYPASS   BR    R14
         DS    0F
FLAG     DC    X'FFFFFFFF'
USERSTAT WTO   'SUN002I JOB          HAD A JCL ERROR',                 X
               ROUTCDE=(15),DESC=(11),MF=L
ABMSG    EQU   *-USERSTAT
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0F
USERAUTO DS    CL(ABMSG)
DATAEND  EQU   *-DATAAREA
MSGTEXT  DSECT
MSGID    DS    CL8
JOBDATA  DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         IEZVX100
         END
MPFVINIT TITLE 'MESSAGE PROCESSOR TO START TSO'
         SPACE 1
*********************************************************************
*                                                                   *
*   MPFVINIT: MPF ROUTINE TO START  TSO                             *
*                                                                   *
*   EXIT:     NORMAL  -  RC=0                                       *
*                                                                   *
*             ERROR   -  RC=4                                       *
*                                                                   *
*   MODS:     AUG.  9,1988   -   CHANGED GETMAIN TO SUBPOOL 230  RP *
*                                                                   *
*********************************************************************
MPFVINIT CSECT ,
MPFVINIT AMODE 31
MPFVINIT RMODE ANY
         SPACE 2
CMDSVC   EQU   34
K0       EQU   0
K1       EQU   1
K2       EQU   2
K4       EQU   4
K8       EQU   8
K12      EQU   12
K16      EQU   16
         SPACE 2
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
         SAVE  (14,12),,MPFVINIT_&SYSDATE  SAVE REGISTERS
         USING MPFVINIT,R12                SET UP BASE
         USING DATA,R13                    SETUP DATE AREA ADDRESS
         LR    R12,R15                     LOAD BASE REG
         L     R8,K0(R1)                   SAVE INPUT PARM
         GETMAIN RU,LV=DATALEN,SP=230,LOC=BELOW GET STORAGE       (RP)
         ST    R13,K4(R1)                  SAVE CALLERS WORK AREA
         ST    R1,K8(R13)                  SAVE MY WORK AREA
         LR    R13,R1                      LOAD SAVE AREA ADDRESS
         SPACE 1
*   START TSO
         SPACE 1
         MVC   CMDAREA1(CMDLEN1),CMDL1     MOVE IN START COMMAND
         SLR   R0,R0                       CLEAR REG 0
         LA    R1,CMDAREA1                 POINT TO START OF COMMAND
         SVC   CMDSVC                      ISSUE START COMMAND
*   START CICS MONITOR
         MVC   CMDAREA2(CMDLEN2),CMDL2     MOVE IN START COMMAND
         SLR   R0,R0                       CLEAR REG 0
         LA    R1,CMDAREA2                 POINT TO START OF COMMAND
         SVC   CMDSVC                      ISSUE START COMMAND
*   START DTFVTAM
         MVC   CMDAREA3(CMDLEN3),CMDL3     MOVE IN START COMMAND
         SLR   R0,R0                       CLEAR REG 0
         LA    R1,CMDAREA3                 POINT TO START OF COMMAND
         SVC   CMDSVC                      ISSUE START COMMAND
         SPACE 1
         LR    R1,R13                      LOAD GETMAINED AREA
         L     R13,K4(R13)                 POINT TO CALLERS SAVE AREA
         FREEMAIN RU,LV=DATALEN,A=(1),SP=230  FREEMAIN AREA       (RP)
         LM    14,12,12(13)                RESTORE REGS
         LA    R15,0                       SET RC=0
         BSM   0,R14                       GOBACK, IN CALLERS MODE
         EJECT
CMDL1    DS    0F
         DC    AL2(CMDLEN1),AL2(00)        LENGTH OF STRING
         DC    C'START TSO '               COMMAND PLUS BLANK
CMDLEN1  EQU   *-CMDL1
CMDL2    DS    0F
         DC    AL2(CMDLEN2),AL2(00)        LENGTH OF STRING
         DC    C'START TMONXS '            COMMAND PLUS BLANK
CMDLEN2  EQU   *-CMDL2
CMDL3    DS    0F
         DC    AL2(CMDLEN3),AL2(00)        LENGTH OF STRING
         DC    C'START DTFVTAM'            COMMAND PLUS BLANK
CMDLEN3  EQU   *-CMDL3
         SPACE 2
DATA     DSECT
SAVE     DS    18F
CMDAREA1 DS    0F,XL(CMDLEN1)
CMDAREA2 DS    0F,XL(CMDLEN2)
CMDAREA3 DS    0F,XL(CMDLEN3)
DATALEN  EQU   *-DATA
         END
         TITLE 'MPFVTAM - ISSUE NON-DELETABLE MESSAGE VTAM NODE INACT'
MPFVTAM CSECT
MPFVTAM AMODE 31
MPFVTAM RMODE ANY
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         STM   R14,R12,12(R13)        SAVE CALLERS REGS
         BALR  R12,R0                 ESTABLISH BASE
         USING *,R12
         L     R5,0(R1)               --> CTXT
         USING CTXT,R5
         GETMAIN RU,LV=DATAEND,LOC=BELOW,SP=230
         LR    R11,R1                 --> GETMAINED AREA
         USING DATAAREA,R11
         ST    R13,SAVEAREA+4
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         LR    R13,R15
         BAL   R14,ISSUWTO            ISSUE THE MESSAGE
FINISHED EQU   *
         L     R13,4(R13)
         FREEMAIN RU,LV=DATAEND,A=(R11),SP=230 FREE THE STORAGE
         LM    R14,R12,12(R13)        RESTORE CALLERS REGS
         LA    R15,0                  SET RETURN CODE
         BR    R14                    GO BACK TO CALLER
ISSUWTO  DS    0H
         SR    R10,R10                CLEAR FLAG
         L     R2,CTXTTXPJ
         USING CTXTATTR,R2
         LA    R4,CTXTTMSG
         USING MSGTEXT,R4
         MVC   USERAUTO,USERSTAT
*
* MOVE IN NODENAME
*
         SLR   R6,R6                        LENGTH OF NODENAME
         LA    R7,8                         MAXIMUM NODENAME LENGTH
         LA    R8,USERAUTO+22               TARGET FOR NODENAME
         LA    R9,NODEDATA                START OF NODENAME IN MESSAGE
NODELOOP CLI   0(R9),C' '                   END OF NODENAME?
         BE    NODEWTO
         MVC   0(1,R8),0(R9)                MOVE 1 CHARACTER AT A TIME
         LA    R6,1(,R6)                    ADD 1 TO LENGTH
         LA    R8,1(,R8)                    BUMP
         LA    R9,1(,R9)                          POINTERS
         BCT   R7,NODELOOP                  MAX NODENAME?
*
         DROP  R2,R4
NODEWTO  WTO   MF=(E,USERAUTO)
BYPASS   BR    R14
         DS    0F
FLAG     DC    X'FFFFFFFF'
USERSTAT WTO   'SUN105I VTAM NODE          HAS GONE INACTIVE',         X
               ROUTCDE=(8),DESC=(11),MF=L
ABMSG    EQU   *-USERSTAT
DATAAREA DSECT
         DS    0F
SAVEAREA DS    18F
         DS    0F
USERAUTO DS    CL(ABMSG)
DATAEND  EQU   *-DATAAREA
MSGTEXT  DSECT
MSGID    DS    CL8
NODEDATA DS    CL8
         ORG   MSGTEXT
         DS    CL4
WTORID   DS    CL8
         IEZVX100
         END
